BASE <http://trans.example.com/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX simcore: <http://www.opteemal.eu/schema/2017/01/SimModelCore#>
PREFIX simgeom: <http://www.opteemal.eu/schema/2017/01/ResourcesGeometry#>
PREFIX list: <https://w3id.org/list#> 
PREFIX inst:  <http://opteemal-project.eu/resource/simmodel#>

# last modification: 2019.07.07

GENERATE
{
	GENERATE
	{
		# Location		
		GENERATE
		{
			
			?IfcLocalPlacement_IRI 
				rdf:type 						simgeom:SimLocalPlacement_LocalPlacement_AbsolutePlacement ;
				simgeom:relativePlacement 		?IfcAxis2Placement3D_IRI ;
				simcore:simModelType 			"LocalPlacement"^^xsd:string ;
				simcore:simModelSubtype 		"AbsolutePlacement"^^xsd:string ;
				simcore:isTemplateObject 		"false"^^xsd:boolean ;
				simcore:simUniqueID             "not defined" ;
				simcore:sourceModelObjectType  	"IfcLocalPlacement" ;
				simcore:sourceModelSchema 		"IFC4" ;
				simcore:refId 					?IfcLocalPlacementRefId ;
				.		
			?IfcAxis2Placement3D_IRI 
				rdf:type 						simgeom:SimPlacement_Axis2Placement3D_Default ;
				simgeom:axis                    ?IfcDirectionAxis_IRI ;
				simgeom:refDirection           	?IfcDirection_IRI ;
				simgeom:location               	?IfcCartesianPoint_IRI ;
				simcore:isTemplateObject 		"false"^^xsd:boolean ;
				simcore:simModelSubtype        	"Default"^^xsd:string ;
				simcore:simModelType 		   	"Axis2Placement3D"^^xsd:string ;
				simcore:simUniqueID            	"not defined" ;
				simcore:sourceModelObjectType  	"IfcAxis2Placement3D" ;
				simcore:sourceModelSchema      	"IFC4" ;
				simcore:refId 					?IfcAxis2Placement3DRefId ;
				.				
			?IfcDirectionAxis_IRI						
				rdf:type 						simgeom:SimGeomVector_Vector_Direction ;
				.
			
			?IfcCartesianPoint_IRI 
				rdf:type 							simgeom:SimGeomPoint_Point_CartesianPoint ;
				simcore:simModelType 				"Point"^^xsd:string ;
				simcore:simModelSubtype 			"CartesianPoint"^^xsd:string ;
				simcore:isTemplateObject 			"false"^^xsd:boolean ;
				simcore:sourceModelObjectType 		"IfcCartesianPoint" ;
				simcore:sourceModelSchema 			"IFC4" ;
				simcore:simUniqueID 				"not defined" ;
				simgeom:coordinates_MeasureValue 	?IfcLengthMeasure_List_0_IRI ;		
				simcore:refId 						?IfcCartesianPointRefId ;
				.	
		
			# List LOCATION
			GENERATE 
			{
				?IfcLengthMeasure_List_IRI	
					rdf:type            	simcore:DoubleList ;
					list:hasContents 		?IfcLengthMeasureValue ;
					list:hasNext 			?IfcLengthMeasure_List_IRI_NEXT ;
					.
			}
			ITERATOR iter:XPath(?IfcCartesianPoint, "/IfcCartesianPoint/Coordinates/IfcLengthMeasure" ) AS ?IfcLengthMeasure ?pos_ILM ?hasNext_ILM
			WHERE
			{
				BIND( IRI( CONCAT( ?base, "IfcLengthMeasure_List_",(?IfcCartesianPoint_ID + "_{str(?pos_ILM+1)}")) ) AS ?IfcLengthMeasure_List_IRI )
				BIND( IRI( CONCAT( ?base, "IfcLengthMeasure_List_",(?IfcCartesianPoint_ID + "_{str(?pos_ILM+2)}")) ) AS ?IfcLengthMeasure_List_IRI_NEXT_TEMP )
				BIND( IF( ?hasNext_ILM, ?IfcLengthMeasure_List_IRI_NEXT_TEMP , ?undef ) AS ?IfcLengthMeasure_List_IRI_NEXT ) 
				BIND( xsd:double(fn:XPath(?IfcLengthMeasure,"//IfcLengthMeasure/text()")) AS ?IfcLengthMeasureValue )
			}.
		
			# RefDirection (OPTIONAL)
			GENERATE
			{
				?IfcDirection_IRI
					rdf:type 						simgeom:SimGeomVector_Vector_Direction ;
					simgeom:directionRatios 		?IfcReal_List_0_IRI ;
					simcore:simModelType 			"Vector"^^xsd:string ;
					simcore:simModelSubtype 		"Direction"^^xsd:string ;
					simcore:sourceModelSchema 		"IFC4"^^xsd:string ;
					simcore:sourceModelObjectType 	"SimGeomVector"^^xsd:string ;
					simcore:isTemplateObject 		"false"^^xsd:boolean ;
					simcore:refId 					?IfcDirectionRefId ;
					simcore:simUniqueID 			"not defined" ;
					.
				
				# List Direction				
				GENERATE 
				{
					?IfcReal_List_IRI 
						rdf:type 				simcore:DoubleList ;
						list:hasContents 		?IfcRealValue ;
						list:hasNext 			?IfcRealNode_List_Last ;
						.
				}
				ITERATOR iter:XPath(?IfcDirection, "/IfcDirection/DirectionRatios/IfcReal" ) AS ?IfcReal ?pos_ILM_Real ?hasNext_ILM_Real
				WHERE
				{
					BIND( IRI( CONCAT( ?base, "IfcReal_List_",(?IfcDirection_ID + "_{str(?pos_ILM_Real+1)}")) ) AS ?IfcReal_List_IRI )
					BIND( IRI( CONCAT( ?base, "IfcReal_List_",(?IfcDirection_ID + "_{str(?pos_ILM_Real+2)}")) ) AS ?IfcReal_List_IRI_NEXT_TEMP )
					BIND( IF( ?hasNext_ILM_Real, ?IfcReal_List_IRI_NEXT_TEMP , ?undef ) AS ?fcReal_List_IRI_NEXT ) 
					BIND( xsd:double(fn:XPath(?IfcReal,"//IfcReal/text()")) AS ?IfcRealValue )		
				}.
			}
			ITERATOR iter:XPath(?IfcAxis2Placement3D, "/IfcAxis2Placement3D/RefDirection/IfcDirection" ) AS ?IfcDirection 
			WHERE
			{
				BIND( fn:XPath(?IfcDirection,"/IfcDirection/@id") AS ?IfcDirection_ID )
				BIND( IRI( CONCAT( ?base, "IfcDirection_", ?IfcDirection_ID ) ) AS ?IfcDirection_IRI )
				BIND( (xsd:ID(CONCAT("ID", ?IfcDirection_ID))) as ?IfcDirectionRefId ) . 
				
				BIND( IRI( CONCAT( ?base, "IfcReal_List_",(?IfcDirection_ID + "_1")) ) AS ?IfcReal_List_0_IRI )
			}.
		}
		ITERATOR iter:XPath(?IfcAxis2Placement3D, "/IfcAxis2Placement3D/Location/IfcCartesianPoint" ) AS ?IfcCartesianPoint 
		WHERE
		{
			BIND( fn:XPath(?IfcCartesianPoint,"/IfcCartesianPoint/@id") AS ?IfcCartesianPoint_ID )
			BIND( IRI( CONCAT( ?base, "IfcCartesianPoint_", ?IfcCartesianPoint_ID ) ) AS ?IfcCartesianPoint_IRI )
			BIND( (xsd:ID(CONCAT("ID", ?IfcCartesianPoint_ID))) as ?IfcCartesianPointRefId ) . 
			
			BIND( IRI( CONCAT( ?base, "IfcLengthMeasure_List_",(?IfcCartesianPoint_ID + "_1")) ) AS ?IfcLengthMeasure_List_0_IRI )
		}.
	}
	ITERATOR iter:XPath(?IfcLocalPlacement, "/IfcLocalPlacement/RelativePlacement/IfcAxis2Placement3D" ) AS ?IfcAxis2Placement3D
	WHERE
	{
		BIND( fn:XPath(?IfcAxis2Placement3D,"/IfcAxis2Placement3D/@id") AS ?IfcAxis2Placement3D_ID )
		BIND( IRI( CONCAT( ?base, "IfcAxis2Placement3D_", ?IfcAxis2Placement3D_ID ) ) AS ?IfcAxis2Placement3D_IRI )
		BIND( (xsd:ID(CONCAT("ID", ?IfcAxis2Placement3D_ID))) as ?IfcAxis2Placement3DRefId ) .
	}.
}

SOURCE <http://ex.org/file1> AS ?source
# This name variable is mapped to a file path in a new line that will be 
# included by the program linked to the original file:

ITERATOR iter:XPath(?source, "//ifc_uos/IfcLocalPlacement" ) AS ?IfcLocalPlacement
WHERE 
{
	# 1. Extract base URI:
	BIND (str(inst:) as ?base)
	BIND( fn:XPath(?IfcLocalPlacement,"/IfcLocalPlacement/@id" ) AS ?IfcLocalPlacement_ID )	
	BIND( IRI( CONCAT( ?base, "IfcLocalPlacement_", ?IfcLocalPlacement_ID ) ) AS ?IfcLocalPlacement_IRI )
	BIND( (xsd:ID(CONCAT("ID", ?IfcLocalPlacement_ID))) as ?IfcLocalPlacementRefId ) . 
		
	FILTER NOT EXISTS { 
		BIND( fn:XPath(?IfcLocalPlacement,"/IfcLocalPlacement/PlacementRelTo/IfcLocalPlacement/@ref" ) AS ?PlacementRelTo_REF )	  
		FILTER (strlen(?PlacementRelTo_REF)>1)
	}	
}
















