BASE <http://trans.example.com/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX simcore: <http://www.opteemal.eu/schema/2017/01/SimModelCore#>
PREFIX simres: <http://www.opteemal.eu/schema/2017/01/ResourcesGeneral#>
PREFIX inst:  <http://opteemal-project.eu/resource/simmodel#>

# last modification: 2019.06.07

GENERATE
{
	?IfcIrregularTimeSeries_IRI	
		rdf:type 							simres:SimIrregularTimeSeries ;
		simres:name                    		?name ;
		simcore:sourceModelObjectType 		"IfcIrregularTimeSeries"^^xsd:string ;	
		simcore:sourceModelSchema 			"IFC4"^^xsd:string ;
		simcore:isTemplateObject 			"false"^^xsd:boolean ;
		simres:startTime 					?StartTime ;
		simres:endTime 						?EndTime ;
		simres:timeSeriesDataType 			?TimeSeriesDataType;
		simres:dataOrigin 					?DataOrigin;
		simcore:refId 						?IfcIrregularTimeSeriesRefId ;
		simcore:simUniqueID 				"not defined" ;
		simres:values 						?IfcIrregularTimeSeriesValue_List_IRI ;
		.
}

SOURCE <http://ex.org/file1> AS ?source
# This name variable is mapped to a file path in a new line that will be 
# included by the program linked to the original file:

ITERATOR iter:XPath(?source, "//ifc_uos/IfcIrregularTimeSeries" ) AS ?IfcIrregularTimeSeries
WHERE 
{
    # 1. Extract base URI:
	BIND (str(inst:) as ?base)
	
	# 2. Extract element names:
	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/@id") AS ?IfcIrregularTimeSeries_ID )
	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/Name/text()") AS ?name )
	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/StartTime/text()") AS ?StartTime )
	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/EndTime/text()") AS ?EndTime )
	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/TimeSeriesDataType/text()") AS ?TimeSeriesDataType )
	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/DataOrigin/text()") AS ?DataOrigin )

	BIND( fn:XPath(?IfcIrregularTimeSeries,"/IfcIrregularTimeSeries/Values/IfcIrregularTimeSeriesValue[1]/@id") AS ?IfcIrregularTimeSeriesValue_0_ID )
	BIND( IRI( CONCAT( ?base, CONCAT("IfcIrregularTimeSeriesValue_List_", ?IfcIrregularTimeSeriesValue_0_ID), "_0")) AS ?IfcIrregularTimeSeriesValue_List_IRI )
	
	BIND( IRI( CONCAT( ?base, "IfcIrregularTimeSeries_", ?IfcIrregularTimeSeries_ID ) ) AS ?IfcIrregularTimeSeries_IRI )
	BIND( (xsd:ID(CONCAT("ID", ?IfcIrregularTimeSeries_ID))) as ?IfcIrregularTimeSeriesRefId )
}


