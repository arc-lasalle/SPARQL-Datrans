BASE <http://trans.example.com/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX simcore: <http://www.opteemal.eu/schema/2017/01/SimModelCore#>
PREFIX simres: <http://www.opteemal.eu/schema/2017/01/ResourcesGeneral#>
PREFIX inst:  <http://opteemal-project.eu/resource/simmodel#>

# last modification: 2019.07.17

GENERATE 
{
	?IfcSIUnit_IRI 
		rdf:type 						simres:SimSIUnitType_SiUnit_Default ;
		simcore:isTemplateObject 		"false"^^xsd:boolean ;
		simcore:simModelType 			"SiUnit"^^xsd:string ;
		simcore:simModelSubtype 		"Default"^^xsd:string ;
		simcore:sourceModelSchema 		"IFC4"^^xsd:string ;
		simcore:sourceModelObjectType 	"IfcSIUnit"^^xsd:string ;
		simcore:simUniqueID 			"not defined" ;
		simcore:refId 					?IfcSIUnitRefId ;
		.
		
	GENERATE 
	{	
		?IfcDerivedUnit_IRI 
			rdf:type 						simres:SimSIUnitType_SiUnit_Default ;
			simcore:isTemplateObject 		"false"^^xsd:boolean ;
			simcore:simModelType 			"SiUnit"^^xsd:string ;
			simcore:simModelSubtype 		"Default"^^xsd:string ;
			simcore:sourceModelSchema 		"IFC4"^^xsd:string ;
			simcore:sourceModelObjectType 	"IfcDerivedUnit"^^xsd:string ;
			simcore:simUniqueID 			"not defined" ;
			simcore:refId 					?IfcDerivedUnitRefId ;
			.
	}
	ITERATOR iter:XPath(?source, "//ifc_uos/IfcDerivedUnit" ) AS ?IfcDerivedUnit
	WHERE 
	{
		BIND( fn:XPath(?IfcDerivedUnit,"/IfcDerivedUnit/@id" ) AS ?IfcDerivedUnit_ID )
		BIND( IRI( CONCAT( ?base, "IfcDerivedUnit_", ?IfcDerivedUnit_ID ) ) AS ?IfcDerivedUnit_IRI )
		BIND( (xsd:ID(CONCAT("ID", ?IfcDerivedUnit_ID))) as ?IfcDerivedUnitRefId ) . 
	}.	
}

SOURCE <http://ex.org/file1> AS ?source
# This name variable is mapped to a file path in a new line that will be 
# included by the program linked to the original file:

ITERATOR iter:XPath(?source, "//ifc_uos/IfcSIUnit" ) AS ?IfcSIUnit
WHERE 
{
	BIND (str(inst:) as ?base)
	BIND( fn:XPath(?IfcSIUnit,"/IfcSIUnit/@id" ) AS ?IfcSIUnit_ID )
	BIND( IRI( CONCAT( ?base, "IfcSIUnit_", ?IfcSIUnit_ID ) ) AS ?IfcSIUnit_IRI )
	BIND( (xsd:ID(CONCAT("ID", ?IfcSIUnit_ID))) as ?IfcSIUnitRefId ) . 
}



